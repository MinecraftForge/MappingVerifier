plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group = 'net.minecraftforge'
version = gitVersion()
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true, tags:true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include '**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.mappingverifier.Main')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.mappingverifier.Main')
    manifest.attributes('Implementation-Version': project.version)
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
}


repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.0'
    implementation 'org.ow2.asm:asm-tree:9.0'
    implementation 'org.ow2.asm:asm-util:9.0'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
	//Because tons of projects all paarsing SRG files is annoying
	implementation 'net.minecraftforge:srgutils:0.4.+'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact shadowJar
            pom {
                name = 'MappingVerifier'
                description = 'Utility to verify mapping files do not mess up bytecode access/layout.'
                url = 'https://github.com/MinecraftForge/MappingVerifier'
                scm {
                    url = 'https://github.com/MinecraftForge/MappingVerifier'
                    connection = 'scm:git:git://github.com/MinecraftForge/MappingVerifier.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/MappingVerifier.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/MappingVerifier/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('forgeMavenPassword')) {
                credentials {
                    username project.properties.forgeMavenUser
                    password project.properties.forgeMavenPassword
                }
                url 'https://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
